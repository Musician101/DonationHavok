buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
    }
}

apply plugin: "net.minecraftforge.gradle.forge"
apply from: "dh.gradle"

version = modVersion
group = modGroup
archivesBaseName = modBaseName

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

configurations {
    shade
    compile.extendsFrom shade
}

dependencies {
    shade('org.reflections:reflections:0.9.10') {
        exclude group: 'com.google.guava'
        exclude group: 'com.google.code.findbugs'
    }
}

jar {
    configurations.shade.each {
        from(it.isDirectory() ? it : project.zipTree(it)) {
            exclude "META-INF/**"
        }
    }
}

reobfJar {
    addExtraSrgLine "PK: org/reflections io/musician101/donationhavok/lib/org/reflections"
    addExtraSrgLine "PK: javassist io/musician101/donationhavok/lib/javassist"
}

minecraft {
    version = project.forgeVersion
    runDir = "run"

    replaceIn "Reference.java"
    replace "@VERSION@", "${modVersion}"

    replaceIn "TwitchBot.java"
    replace "@TOKEN@", "${project.ext.dhToken}"

    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = project.mcpVersion
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        // replace version and mcversion
        expand "version": project.version, "mcversion": project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}
